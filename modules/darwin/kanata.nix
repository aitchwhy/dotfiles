# Kanata keyboard remapper - system daemon
# Runs as root to access keyboard devices without Input Monitoring permission
# Requires Karabiner driver: brew install karabiner-elements
#
# Config is generated by home-manager (modules/home/apps/kanata.nix)
# This module manages the launchd daemon and auto-restart on switch
{
  config,
  lib,
  pkgs,
  ...
}:
let
  inherit (lib) mkEnableOption mkIf;
in
{
  options.modules.darwin.kanata = {
    enable = mkEnableOption "Kanata keyboard remapper daemon";
  };

  config = mkIf config.modules.darwin.kanata.enable {
    # System-level LaunchDaemon (runs as root)
    launchd.daemons.kanata = {
      serviceConfig = {
        Label = "org.kanata.daemon";
        ProgramArguments = [
          "${pkgs.kanata}/bin/kanata"
          "--cfg"
          "/Users/${config.system.primaryUser}/.config/kanata/kanata.kbd"
        ];
        RunAtLoad = true;
        KeepAlive = true;
        StandardOutPath = "/tmp/kanata.log";
        StandardErrorPath = "/tmp/kanata.err";
      };
    };

    # Restart daemons after activation (config may have changed)
    # This runs at the end of every `darwin-rebuild switch`
    # Note: Fully detach with nohup + redirect to avoid blocking darwin-rebuild
    system.activationScripts.postActivation.text = ''
      echo "Restarting Kanata daemon..."
      nohup /bin/launchctl kickstart -k system/org.kanata.daemon >/dev/null 2>&1 &
    '';
  };
}
